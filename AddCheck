Sub AddCheck()
  Dim txt As String, rng As Range, u As Long
  Application.Calculation = xlCalculationManual
  Application.ScreenUpdating = False
  Set rng = Selection
  'copy sheet if necessary
  If SheetExists("CheckSheet") = False Then
    On Error Resume Next
      Sheets.Add before:=ActiveWorkbook.Sheets(1)
      'would normally use error check here, but some PCs throw up spurious errors to have to use indirect methods to see it it worked
      'used range of one cell, or sheet name not starting with sheet => error
      If ActiveSheet.UsedRange.Cells.count > 1 Or InStr(1, ActiveSheet.Name, "sheet", vbTextCompare) = 0 Then
        MsgBox "I couldn't create a checksheet - the error was: " & err.Description
        Exit Sub
      End If
      Cells.VerticalAlignment = xlTop
      Cells.Columns(1).HorizontalAlignment = xlCenter
    On Error GoTo 0
    ActiveSheet.Name = "CheckSheet"
    SetupCell Cells(1, 1), "Issue #", 8.5, False
    SetupCell Cells(1, 2), "Sheet", 18, False
    SetupCell Cells(1, 3), "Cells", 15, False
    SetupCell Cells(1, 4), "Issue", 70, True
    SetupCell Cells(1, 5), "User", 6, True
    SetupCell Cells(1, 6), "Date", 11, False
    SetupCell Cells(1, 7), "Action", 30, True
    SetupCell Cells(1, 8), "User", 6, True
    SetupCell Cells(1, 9), "Date", 11, False
    For u = 1 To 50
      Cells(u + 1, 1) = u
    Next u
    Cells(2, 1).Select
    ActiveWindow.FreezePanes = True
  End If
  'add new issue
  Dim r As Long, rngName As String
  With ActiveWorkbook.Sheets("CheckSheet")
    r = .Cells(1000, 2).End(xlUp).Row + 1
    .Cells(r, 2) = rng.Parent.Name
    .Cells(r, 3) = rng.Address
    .Cells(r, 5) = GetUserInitials
    .Cells(r, 6) = "'" & Format(Now, "d/m/yy")
    rngName = "lnk" & Format(Now, "ddmmyyhhmmss")
    Names.Add Name:=rngName, RefersTo:=rng
    Names(rngName).visible = False  '
    .Hyperlinks.Add Anchor:=.Cells(r, 3), Address:="", SubAddress:=rngName
    .Activate
    .Cells(r, 4).Select
  End With
  Application.Calculation = xlCalculationAutomatic
End Sub
